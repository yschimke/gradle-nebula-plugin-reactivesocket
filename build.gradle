/*
 * Copyright 2014-2016 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript { repositories { jcenter() } } // Some plugin dependencies (namely JFrog) are not in Maven Central

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'nebula.info' version '3.0.3'
    id 'nebula.contacts' version '3.0.1'
    id 'nebula.maven-publish' version '4.4.4'
    id 'nebula.nebula-release' version '4.0.1'
    id 'nebula.nebula-bintray' version '3.3.2'
    id 'nebula.dependency-lock' version '4.2.0'
    id 'com.gradle.plugin-publish' version '0.9.3'
    id 'nebula.javadoc-jar' version '4.4.4'
    id 'nebula.source-jar' version '4.4.4'
    id 'nebula.maven-apache-license' version '4.4.4'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

description 'Template for ReactiveSocket projects'

contacts {
    'benjchristensen@gmail.com' {
        moniker 'Ben Christensen'
        github 'benjchristensen'
    }
}

bintray {
  publish = true
  dryRun = false
  pkg {
    repo = 'ReactiveSocket'
    userOrg = 'reactivesocket'
    name = project.name
    desc = project.description
    websiteUrl = 'https://github.com/ReactiveSocket/${project.name}'
    issueTrackerUrl = 'https://github.com/ReactiveSocket/${project.name}/issues'
    vcsUrl = 'https://github.com/ReactiveSocket/${project.name}.git'
    licenses = ['Apache-2.0']
    labels = ['reactivesocket', 'ReactiveSocket']
    publicDownloadNumbers = true
    attributes = [:]
    version {
      name = project.version
      vcsTag = project.version
      attributes = [:]
    }
  }
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    compile 'com.netflix.nebula:nebula-core:3.+'
    compile 'com.netflix.nebula:nebula-project-plugin:3.+'
    compile 'com.netflix.nebula:nebula-bintray-plugin:3.+' // for publishing
    compile 'com.netflix.nebula:nebula-release-plugin:3.+' // for releasing
    compile 'com.netflix.nebula:gradle-override-plugin:2.+'
    compile 'com.netflix.nebula:nebula-publishing-plugin:4.+' // for publishing
    compile 'com.github.jengelman.gradle.plugins:shadow:1.+'
    compile 'nl.javadude.gradle.plugins:license-gradle-plugin:0.+'
    compile 'com.google.guava:guava:17.0' // For ClasspathHelper

    testCompile('com.netflix.nebula:nebula-test:4.+') {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
}

test {
    maxHeapSize = '512m'
}

pluginBundle {
    plugins {
        reactivesocketProject {
            id = 'reactivesocket-project'
            displayName = 'ReactiveSocket Project Template'
            description = project.description
            tags = ['reactivesocket', 'project']
        }
    }
}